plugins {
    // Main java library
    id 'java'

    // Shadow jar to relocate jars
    // Shadow gradle is broken currently so using googler's one works for now.
    //id 'com.github.johnrengelman.shadow' version '8.1.1'
    id("io.github.goooler.shadow") version "8.1.8"
    id 'io.freefair.lombok' version '8.6'
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.16"
    id("xyz.jpenilla.run-paper") version "2.3.0"
}

var minecraftServerVersion = '1.21.4'
group = 'xyz.goldendupe'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' } // Vault

    maven {
        url "https://libraries.minecraft.net"
    }


    // Cloud snapshots
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
        name "sonatype-snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}


dependencies {
    implementation 'com.github.AstralLiteratureClub:MoreForJava:-SNAPSHOT'
    implementation 'com.github.AstralLiteratureClub:MessageManager:-SNAPSHOT'
    implementation 'com.github.AstralLiteratureClub:GUIMan:-SNAPSHOT'
    implementation 'com.github.AstralLiteratureClub:ChatGameCore:-SNAPSHOT'
    implementation 'com.github.AstralLiteratureClub:CloudPlusPlus:-SNAPSHOT'

    //compileOnly 'org.reflections:reflections:0.10.2' // Reflections - Use classgraph for faster reflections
    compileOnly 'io.github.classgraph:classgraph:4.8.165'

    // Cloud and brigadier for commands
    compileOnly 'com.mojang:brigadier:1.0.18'
    var cloudMineccraft = "2.0.0-beta.10";
    implementation 'org.incendo:cloud-paper:' + cloudMineccraft
    implementation 'org.incendo:cloud-brigadier:' + cloudMineccraft
    implementation 'org.incendo:cloud-minecraft-extras:' + cloudMineccraft

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    compileOnly 'org.apache.commons:commons-math3:3.6.1'

    compileOnly "com.github.MilkBowl:VaultAPI:1.7"  // Vault
    compileOnly 'net.luckperms:api:5.4' // Luckperms

    // Scoreboard
    var scoreboardLibraryVersion = "2.2.2"
    implementation("net.megavex:scoreboard-library-api:$scoreboardLibraryVersion")
    runtimeOnly("net.megavex:scoreboard-library-implementation:$scoreboardLibraryVersion")

    // Add packet adapter implementations you want:
    runtimeOnly("net.megavex:scoreboard-library-modern:$scoreboardLibraryVersion:mojmap") // Mojang mapped variant (only use if you know what you're doing!)

    // Other Dependencies
    paperweight.paperDevBundle(minecraftServerVersion+"-R0.1-SNAPSHOT")
}

// Some shit is importing 1.13 bukkit api and not 1.20.1 api
configurations.compileOnly {
    exclude group: 'org.spigotmc', module: 'spigot'
    exclude group: 'org.spigotmc', module: 'spigot-api'
    exclude group: 'org.bukkit', module: 'bukkit'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(22))
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['*.yml']) {
        expand([version: version])
    }
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier = null
    relocate 'bet.astral.messenger', 'xyz.goldendupe.libs.messenger'
    relocate 'bet.astral.cloudplusplus', 'xyz.goldendupe.libs.cloudplusplus'
    relocate 'bet.astral.tuples', 'xyz.goldendupe.libs.tuples'
    relocate 'org.incendo.cloud', 'xyz.goldendupe.libs.org.incendo.cloud'
    relocate 'net.magavex', "xyz.goldendupe.libs.net.megavex"
}

tasks {
    runServer {
        minecraftVersion(minecraftServerVersion)
    }
}