plugins {
    id 'java'

    id("io.github.goooler.shadow") version "8.1.8"
    id 'io.freefair.lombok' version '8.6'
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.17"
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

def minecraftServerVersion = '1.21.8'
def groupProject = 'bet.astral.wormhole'
group = 'bet.astral'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' } // Vault

    maven {
        url "https://libraries.minecraft.net"
    }


    // Cloud snapshots
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
        name "sonatype-snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}


dependencies {
    implementation 'com.github.AstralLiteratureClub:MoreForJava:1.0.2'
    implementation 'com.github.AstralLiteratureClub:MessageManager:2.4.1'
    implementation 'com.github.AstralLiteratureClub:GUIMan:1.3.1-2'
    implementation 'com.github.AstralLiteratureClub:ChatGameCore:1.0.1'
    implementation 'com.github.AstralLiteratureClub:CloudPlusPlus:1.3.0'

    //compileOnly 'org.reflections:reflections:0.10.2' // Reflections - Use classgraph for faster reflections
    implementation 'io.github.classgraph:classgraph:4.8.165'

    // Cloud and brigadier for commands
    compileOnly 'com.mojang:brigadier:1.0.18'
    var cloud = "2.0.0";
    var cloudMinecraft = "2.0.0-beta.11";
    implementation 'org.incendo:cloud-paper:' + cloudMinecraft
    implementation 'org.incendo:cloud-brigadier:' + cloudMinecraft
    implementation 'org.incendo:cloud-minecraft-extras:' + cloudMinecraft
    implementation("org.incendo:cloud-translations-core:1.0.0-beta.2")
    implementation("org.incendo:cloud-translations-bukkit:1.0.0-beta.2")

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    paperweight.paperDevBundle(minecraftServerVersion+"-R0.1-SNAPSHOT")
}

// Some shit is importing 1.13 bukkit api and not 1.20.1 api
configurations.compileOnly {
    exclude group: 'org.spigotmc', module: 'spigot'
    exclude group: 'org.spigotmc', module: 'spigot-api'
    exclude group: 'org.bukkit', module: 'bukkit'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(22))
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['*.yml']) {
        expand([version: version])
    }
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier = null
    relocate 'bet.astral.more4j', groupProject+'.libs.astral.more4j'
    relocate 'bet.astral.more4jmc', groupProject+'.libs.astral.more4jmc'
    relocate 'bet.astral.chatgamecore', groupProject+'.libs.astral.chatgamecore'
    relocate 'bet.astral.cloudplusplus', groupProject+'.libs.astral.cloudplusplus'
    relocate 'bet.astral.guiman', groupProject+'.libs.astral.guiman'
    relocate 'bet.astral.messenger', groupProject+'.libs.astral.messenger'
    relocate 'org.incendo.cloud', groupProject+'.libs.org.incendo.cloud'
    relocate 'io.github.classgraph', groupProject+"libs.io.github.classgraph"
}

tasks {
    runServer {
        minecraftVersion(minecraftServerVersion)
    }
}